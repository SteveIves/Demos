import System.Globalization
import System.Windows
import System.Windows.Markup
import DemoApp.ViewModel
import System

namespace DemoApp
	
	public partial class App extends Application
		
		static method App
		proc
			;;This code is used to test the app when using other cultures.
			;;
			;;System.Threading.Thread.CurrentThread.CurrentCulture =
			;;&	System.Threading.Thread.CurrentThread.CurrentUICulture =
			;;&		new System.Globalization.CultureInfo("it-IT")
		
			;;Ensure the current culture passed into bindings is the OS culture.
			;;By default, WPF uses en-US as the culture, regardless of the system settings.
			FrameworkElement.LanguageProperty.OverrideMetadata(
			&		^typeof(FrameworkElement),
			&		new FrameworkPropertyMetadata(XmlLanguage.GetLanguage(CultureInfo.CurrentCulture.IetfLanguageTag))
			&	)

		endmethod
		
		protected viewModel	,@MainWindowViewModel
		protected wnd		,@MainWindow
		
		protected override method OnStartup, void
			required in e, @StartupEventArgs
			endparams
		proc
			parent.OnStartup(e)
		
			wnd = new MainWindow()
		
			;;Create the ViewModel to which the main window binds.
			viewModel = new MainWindowViewModel("Data/customers.xml")
		
			addhandler(viewModel.RequestClose, this.Close)
		
			;;Allow all controls in the window to bind to the ViewModel by setting the 
			;;;DataContext, which propagates down the element tree.
			wnd.DataContext = viewModel
		
			;;Show the window
			wnd.Show()
		
		endmethod
		
		protected method Close, Void
			sender, @*
			e, @EventArgs
			endparams
		proc
			removehandler(viewModel.RequestClose, this.Close)
			wnd.close()
		endmethod
			
	endclass

endnamespace
			