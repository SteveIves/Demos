
import System
import System.Collections.ObjectModel
import System.ComponentModel
import System.ServiceModel
import System.Threading.Tasks
import System.Windows.Input
import TestClient.PartsService
import System.Linq

namespace TestClient

	public class ViewModel implements INotifyPropertyChanged

.region "Constructors"

		public method ViewModel
			endparams
		proc
			getSuppliers()
		endmethod

.endregion

.region "Backing storage for properties"

		private mSuppliers,			@ObservableCollection<Supplier>
		private mSelectedSupplier,	@Supplier
		private mParts,				@ObservableCollection<Part>
		private mSelectedPart,		@Part

.endregion

.region "Bindable Properties"

		public property Suppliers, @ObservableCollection<Supplier>
			method get
			proc
				mreturn mSuppliers
			endmethod
			method set
            proc
				mSuppliers = value
				NotifyPropertyChanged("Suppliers")
			endmethod
		endproperty
		
		public property SelectedSupplier, @Supplier
			method get
			proc
				mreturn mSelectedSupplier
			endmethod
			method set
			proc
				mSelectedSupplier = value
				NotifyPropertyChanged("SelectedSupplier")
				getSupplierParts()
			endmethod
		endproperty
		
		public property Parts, @ObservableCollection<Part>
			method get
			proc
				mreturn mParts
			endmethod
			method set
			proc
				mParts = value
				NotifyPropertyChanged("Parts")
			endmethod
		endproperty
		
		public property SelectedPart, @Part
			method get
			proc
				mreturn mSelectedPart
			endmethod
			method set
			proc
				mSelectedPart = value
				NotifyPropertyChanged("SelectedPart")
			endmethod
		endproperty

.endregion

.region "Logic"

		private async method getSuppliers, void
			endparams
		proc
			data client, @PartsServiceClient , new PartsServiceClient()
			
			try
			begin
				ErrorMessage = ""
				data response, @ReadAllSuppliersResponse, await client.ReadAllSuppliersAsync(new ReadAllSuppliersRequest())
				if (response.ReadAllSuppliersResult==MethodStatus.Success)
                    Suppliers = response.aSuppliers
				client.Close()
			end
			catch (ex, @Exception)
			begin
				client.Abort()
				ErrorMessage = ex.Message
			end
			endtry

			if (Suppliers.Count > 0)
				SelectedSupplier = Suppliers[0]
				
		endmethod

		private async method getSupplierParts, void
			endparams
		proc
			data client, @PartsServiceClient , new PartsServiceClient()

			try
			begin
				ErrorMessage = ""
				data response, @ReadSupplierPartsResponse, await client.ReadSupplierPartsAsync(new ReadSupplierPartsRequest(mSelectedSupplier.Supplierid))
				if (response.ReadSupplierPartsResult==MethodStatus.Success)
                    Parts = response.aParts
				client.Close()
			end
			catch (ex, @Exception)
			begin
				client.Abort()
				ErrorMessage = ex.Message
			end
			endtry

			if (Parts.Count>0)
				SelectedPart = Parts[0]

		endmethod

.endregion

.region "ErrorMessage"

		private mErrorMessage, String

		public property ErrorMessage, String
			method get
			proc
				mreturn mErrorMessage
			endmethod
			method set
			proc
				mErrorMessage = value
				NotifyPropertyChanged("ErrorMessage")
			endmethod
		endproperty
		
.endregion

.region "INotifyPropertyChanged"
		
		public event PropertyChanged, @PropertyChangedEventHandler
		
		protected method NotifyPropertyChanged, void
			required in propertyName, String
			endparams
		proc
			if (PropertyChanged!=^null)
				raiseevent(PropertyChanged,this,new PropertyChangedEventArgs(propertyName))
		endmethod
		
.endregion
		
	endclass

endnamespace

