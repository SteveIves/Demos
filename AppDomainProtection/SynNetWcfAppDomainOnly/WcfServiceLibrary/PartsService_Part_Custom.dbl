
import System
import System.Collections.Generic
import System.ServiceModel
import System.Threading.Tasks
import Synergex.SynergyDE.Select
import AppDomainProtection

namespace WcfServiceLibrary
	
	public partial class PartsService
		
		;;; <summary>
		;;; Real all Part records from a supplier.
		;;; </summary>
		;;; <param name="aParts">Returned collection of Part objects (@List<Part>)</param>
		;;; <returns></returns>
		{OperationContract}
		public method ReadSupplierParts, MethodStatus
			required in  aSupplierId, String 
			required out aParts, @List<Part>
			endparams
		proc
			
			data status, MethodStatus, MethodStatus.Success
			
			aParts = new List<Part>()
			
			try
			begin
				data rec, strPart
				foreach rec in new Select(new From("DAT:parts.ism",rec),(Where)rec.Supplierid.eq.aSupplierId)
					aParts.Add(new Part((String)rec))
			end
			catch (e, @Exception)
			begin
				status = MethodStatus.FatalError
			end
			endtry
			
			mreturn status
			
		endmethod
		
	endclass
	
endnamespace
