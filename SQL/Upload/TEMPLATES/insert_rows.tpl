<CODEGEN_FILENAME><structure_name>_insert_rows.dbl</CODEGEN_FILENAME>
;//*****************************************************************************
;//
;// Title:        sql_insert_rows.tpl
;//
;// Description:  Template to generate a Synergy function which inserts multiple
;//               rows into a table in a SQL Server database.
;//
;// Author:       Steve Ives, Synergex Professional Services Group
;//
;// Copyright:    ©Synergex International Inc.  All rights reserved.
;//
;// WARNING:      If you were given this code by a Synergex employee then you
;//               may use and modify it freely to generate code for your
;//               applications. However, you may not under any circumstances
;//               distribute this code, or any modified version of this code,
;//               to any third party without first obtaining written permission
;//               to do so from Synergex. In using this code you accept that it
;//               is provided as is, and without support or warranty. Neither
;//               Synergex or the author accept any responsibility for any
;//               losses or damages of any nature which may arise from the use
;//               of this code. This header information must remain unaltered
;//               in the code at all times. Possession of this code, or any
;//               modified version of this code, indicates your acceptance of
;//               these terms.
;//
;// $Revision: 1 $
;//
;// $Date: 2012-02-15 23:52:19-06:00 $
;//
;;*****************************************************************************
;;
;; Routine:     <structure_name>_insert_rows
;;
;; Author:      <AUTHOR> (<ENV:USERNAME>)
;;
;; Company:     <COMPANY>
;;
;; Created:     <DATE> at <TIME>
;;
;;*****************************************************************************
;;
;; WARNING:     This code was generated by <CODEGEN_VERSION>.  Any changes that
;;              you make to this file will be lost if the code is regenerated.
;;
;; This code is supplied as seen and without warranty or support, and is used
;; at your own risk. Neither the author or Synergex accept any responsability
;; for any loss or damage which may result from the use of this code. This text
;; must remain unaltered in this file at all times. Possession or use of this
;; code, or any modified version of this code, indicates your acceptance of
;; these conditions.
;;
;;*****************************************************************************
;;
;; Possible return values from this routine are:
;;
;;  true    Rows inserted
;;  false   Error (see a_errtxt)
;;
function <structure_name>_insert_rows ,^val

    required in  a_dbchn    ,int    ;;Connected database channel
    required in  a_data     ,int    ;;Memory handle with records to insert
    optional out a_errtxt   ,a      ;;Error text
    optional out a_exception,int    ;;Handle to return exception records
    optional in  a_terminal ,int    ;;Terminal number channel to log errors on
    endparams

    .include "CONNECTDIR:ssql.def"
    .include "<STRUCTURE_NOALIAS>" repository, stack record="<structure_name>"
    .include "<STRUCTURE_NOALIAS>" repository, structure="inpbuf", nofields

    external function
        IsNumeric   ,^val
    endexternal

    .define EXCEPTION_BUFSZ 100

    stack record local_data
        ok          ,boolean    ;;Return status
        dberror     ,int        ;;Database error number
        rows        ,int        ;;Number of rows to insert
        cursor      ,int        ;;Database cursor
        cnt         ,int        ;;Generic counter
        transaction ,int        ;;Transaction in progress
        length      ,int        ;;Length of a string
        ex_ms       ,int        ;;Size of exception array
        ex_mc       ,int        ;;Items in exception array
        continue    ,int        ;;Continue after an error
        errtxt      ,a255       ;;Error message text
    endrecord

    static record
        sql         ,string     ;;SQL statement
    endrecord

proc

    init local_data
    ok = true

    if (^passed(a_exception)&&a_exception)
        clear a_exception

    ;;-------------------------------------------------------------------------
    ;;Figure out how many rows to insert
    ;;
    rows = (%mem_proc(DM_GETSIZE,a_data)/^size(<structure_name>))

    ;;-------------------------------------------------------------------------
    ;;Start a database transaction
    ;;
    if (%ssc_commit(a_dbchn,SSQL_TXON)==SSQL_NORMAL) then
        transaction=1
    else
    begin
        ok = false
        if (%ssc_getemsg(a_dbchn,errtxt,length,,dberror)==SSQL_FAILURE)
            errtxt="Failed to start transaction"
    end

    ;;-------------------------------------------------------------------------
    ;;Open a cursor for the INSERT statement
    ;;
    if (ok)
    begin
        if (!(a)sql)
        begin
            sql = "INSERT INTO <STRUCTURE_NAME> ("
            <FIELD_LOOP>
            & + "<FIELD_SQLNAME><,>"
            </FIELD_LOOP>
            & + ") VALUES(<FIELD_LOOP>:<FIELD#LOGICAL><,></FIELD_LOOP>)"
        end

        if (%ssc_open(a_dbchn,cursor,(a)sql,SSQL_NONSEL,SSQL_STANDARD)==SSQL_FAILURE)
        begin
            ok = true
            if (%ssc_getemsg(a_dbchn,errtxt,length,,dberror)==SSQL_FAILURE)
                errtxt="Failed to open cursor"
        end
    end

    ;;-------------------------------------------------------------------------
    ;;Bind the host variables for data to be inserted
    ;;
    if (ok)
    begin
        if (%ssc_bind(a_dbchn,cursor,<STRUCTURE_FIELDS>,
		<FIELD_LOOP>
		<IF NOTDATE>
		&    <structure_name>.<field_name><,>
		</IF>
		<IF DATE>
		&    ^a(<structure_name>.<field_name>)<,>
		</IF>
		</FIELD_LOOP>
        &   )==SSQL_FAILURE)
        begin
            ok = false
            if (%ssc_getemsg(a_dbchn,errtxt,length,,dberror)==SSQL_FAILURE)
                errtxt="Failed to bind variables"
        end
    end

    ;;-------------------------------------------------------------------------
    ;;Insert the rows into the database
    ;;
    if (ok)
    begin
        for cnt from 1 thru rows
        begin
            ;;Load data into bound record
            <structure_name> = ^m(inpbuf[cnt],a_data)

            ;;Clean up the data
            <FIELD_LOOP>
            <IF ALPHA>
            <field_path>=%atrim(<field_path>)+%char(0)
            </IF>
            <IF DECIMAL>
            if ((!<field_path>)||(!%IsNumeric(^a(<field_path>))))
                clear <field_path>
            </IF>
            <IF DATE>
            if ((!<field_path>)||(!%IsNumeric(^a(<field_path>))))
                ^a(<field_path>(1:1))=%char(0)
            </IF>
            <IF TIME>
            if ((!<field_path>)||(!%IsNumeric(^a(<field_path>))))
                ^a(<field_path>(1:1))=%char(0)
            </IF>
            </FIELD_LOOP>

            ;;Execute the statement
            if (%ssc_execute(a_dbchn,cursor,SSQL_STANDARD)==SSQL_FAILURE)
            begin
                if (%ssc_getemsg(a_dbchn,errtxt,length,,dberror)==SSQL_FAILURE)
                    errtxt="Failed to execute SQL statement"

                clear continue

                ;;Are we logging errors?
                if (^passed(a_terminal)&&(a_terminal))
                begin
                    writes(a_terminal,errtxt(1:length))
                    continue=1
                end

                ;;Are we processing exceptions?
                if (^passed(a_exception))
                begin
                    if (ex_mc==ex_ms)
                    begin
                        if (!a_exception) then
                            a_exception = %mem_proc(DM_ALLOC|DM_STATIC,^size(inpbuf)*(ex_ms=EXCEPTION_BUFSZ))
                        else
                            a_exception = %mem_proc(DM_RESIZ,^size(inpbuf)*(ex_ms+=EXCEPTION_BUFSZ),a_exception)
                    end
                    ^m(inpbuf[ex_mc+=1],a_exception)=<structure_name>
                    continue=1
                end

                if (continue) then
                    nextloop
                else
                begin
                    ok = false
                    exitloop
                end
            end
        end
    end

    ;;-------------------------------------------------------------------------
    ;;Close the database cursor
    ;;
    if (cursor)
    begin
        if (%ssc_close(a_dbchn,cursor)==SSQL_FAILURE)
        begin
            if (ok)
            begin
                ok = false
                if (%ssc_getemsg(a_dbchn,errtxt,length,,dberror)==SSQL_FAILURE)
                    errtxt="Failed to close cursor"
            end
        end
    end

    ;;-------------------------------------------------------------------------
    ;;Commit or rollback the transaction
    ;;
    if (transaction)
    begin
        if (ok) then
        begin
            ;;Success, commit the transaction
            if (%ssc_commit(a_dbchn,SSQL_TXOFF)==SSQL_FAILURE)
            begin
                ok = false
                if (%ssc_getemsg(a_dbchn,errtxt,length,,dberror)==SSQL_FAILURE)
                    errtxt="Failed to commit transaction"
            end
        end
        else
        begin
            ;;There was an error, rollback the transaction
            xcall ssc_rollback(a_dbchn,SSQL_TXOFF)
        end
    end

    ;;-------------------------------------------------------------------------
    ;;If we're returning exceptions then resize the buffer to the correct size
    ;;
    if (^passed(a_exception)&&a_exception)
        a_exception = %mem_proc(DM_RESIZ,^size(inpbuf)*ex_mc,a_exception)

    ;;-------------------------------------------------------------------------
    ;;If there was an error message, return it to the calling routine
    ;;
    if (^passed(a_errtxt))
    begin
        if (ok) then
            clear a_errtxt
        else
            a_errtxt=%atrim(errtxt)+" [Database error "+%string(dberror)+"]"
    end

    freturn ok

endfunction

