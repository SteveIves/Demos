;;*****************************************************************************
;;
;; Title:       replicate.dbl
;;
;; Description: Adds a new transaction record to the replication servers
;;              transaction file
;;
;;*****************************************************************************
;;
.include "REPLICATION_INSTRUCTION" repository, enum, end

subroutine replicate

    required in a_action    ,REPLICATION_INSTRUCTION
    required in a_structure ,a  ;Structure name
    required in a_strkey    ,a  ;Unique key of record affected
    endparams

    .include "REPLICATION" repository, local record = "instruction"

    static record
        chn ,i4
    endrecord

proc

    ;Do we need to open the replication transaction file?
    if (!chn)
        open(chn=0,"U:I","DAT:REPLICATION.ISM")

    ;Populate the new transaction record
    instruction.action = (i)a_action
    instruction.structure_name = a_structure
    instruction.structure_key = a_strkey

    ;And add it to the instructions file
    repeat
    begin
        ;Give the instruction a unique and time sequenced primary key
        instruction.transaction_id = %datetime
        store(chn,instruction) [$ERR_NODUPS=duplicate_key]
        exitloop
duplicate_key,
        sleep 0.1
    end

    xreturn

endsubroutine

