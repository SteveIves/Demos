;*******************************************************************************
;
; Title:        replicate.dbl
;
; Description:  Adds a new transaction record to the replication servers
;               transaction file
;
; Author:       Richard Morris
;
.include "REPLICATION_INSTRUCTION" repository, enum, end

subroutine replicate

    required in a_transaction_type  ,REPLICATION_INSTRUCTION
    required in a_structure         ,a  ;Structure name
    required in a_strkey            ,a  ;Unique key of record affected
    endparams

    .include "REPLICATION" repository, local record = "transaction"

    static record
        io_transaction_chn,      i4
    endrecord

proc

    ;Do we need to open the replication transaction file?
    if (!io_transaction_chn)
        open(io_transaction_chn=0,"U:I","DAT:replication.ism")

    ;Populate the new transaction record
    transaction.instruction = (i)a_transaction_type
    transaction.structure_name = a_structure
    transaction.structure_key = a_strkey

    ;And add it to the transaction log
    repeat
    begin
        ;Give the transaction a unique and time sequenced primary key
        transaction.transaction_id = %datetime
        store(io_transaction_chn,transaction) [$ERR_NODUPS=duplicate_key]
        exitloop
duplicate_key,
        sleep 0.1
    end

    xreturn

endsubroutine

