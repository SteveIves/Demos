;;*****************************************************************************
;;
;; Title:       ChangeTracking.dbl
;;
;; Type:        Class
;;
;; Description: Manipulate change tracking info within ISAM files
;;
;; Author:      Steve Ives
;;
;; Company:     Synergex Professional Services Group
;;
;;*****************************************************************************
;;
;; WARNING:     This code was generated by CodeGen. Any changes that you make
;;              to this file will be lost if the code is regenerated.
;;
;;*****************************************************************************
;;
import System.Collections

namespace IsamTools

	;;; <summary>
	;;; Manipulates change tracking information.
	;;; </summary>
	public class ChangeTracking
		
		private static ch, int
		private static buff, a256
		
		;;; <summary>
		;;; Determine if change tracking is enabled on a file.
		;;; </summary>
		;;; <param name="fileSpec">File spec to check.</param>
		;;; <returns>Returns true if change tracking is enabled.</returns>
		public static method IsEnabled, boolean
			required in fileSpec, string
			endparams
		proc
			open(ch=0,i,"|ctutl -v " + fileSpec)
			reads(ch,buff)
			close ch
			
			using buff select
			("Change tracking information:"),
				mreturn true
			("%ISUTL-E-57: File not found"),
				mreturn false
			("%ISUTL-E-STATUS: Change Tracking not enabled for "),
				mreturn false
			(),
				mreturn false
			endusing

		endmethod
		
		;;; <summary>
		;;; Enables change tracking on a file (ctutl -a).
		;;; </summary>
		;;; <param name="fileSpec">File to enable change tracking on.</param>
		;;; <returns>Returns true if change tracking was enabled.</returns>
		public static method Enable, boolean
			required in fileSpec, string
			endparams
		proc
			open(ch=0,i,"|ctutl -a " + fileSpec)
			reads(ch,buff)
			close ch
			
			using buff select
			("Change Tracking applied"),
				mreturn true
			("File revision 6 or greater required"),
				mreturn false
			("Operation not allowed due to backup mode"),
				mreturn false
			("File not found: "),
				mreturn false
			(),
				mreturn false
			endusing
			
		endmethod
		
		;;; <summary>
		;;; Adds a new snapshot to a file (ctutl -s) and returns the snapshot number.
		;;; </summary>
		;;; <param name="fileSpec">File to create snapshot in.</param>
		;;; <param name="snapShotNumber">Returned new snapshot number.</param>
		;;; <returns>Returns true if a snapshot was successfully added.</returns>
		public static method AddSnapshot, boolean
			required in fileSpec, string
			required out snapShotNumber, int
			endparams
			record 
				ok, boolean
			endrecord
		proc
			ok = true
			open(ch=0,i,"|ctutl -s " + fileSpec)
			reads(ch,buff)

			using buff select
			("Synchronizing under Synbackup protection"),
			begin
				;This happens when a snapshot is added WITH synbackup
				;Example output:
				;Synchronizing under Synbackup protection
				;Adding snapshot #6 at Fri Mar 07 15:04:05 2014
				data spacePos, int, %instr(18,buff," ")
				reads(ch,buff)
				snapShotNumber = %integer(buff(18,spacePos-1))
			end
			("Adding snapshot #"),
			begin
				;This happens when a snapshot is added WITHOUT synbackup
				;Example output:
				;Adding snapshot #7 at Fri Mar 07 15:05:24 2014
				data spacePos, int, %instr(18,buff," ")
				snapShotNumber = %integer(buff(18,spacePos-1))
			end
			("%ISUTL-E-57: File not found"),
				ok = false
			("%ISUTL-E-SYNC: Change Tracking not enabled for"),
				ok = false
			(),
				ok = false
			endusing
			close ch
			mreturn ok
		endmethod
		
		;;; <summary>
		;;; Adds a new snapshot to a file (ctutl -s).
		;;; </summary>
		;;; <param name="fileSpec">File to create snapshot in.</param>
		;;; <returns>Returns true if a snapshot was successfully added.</returns>
		public static method AddSnapshot, boolean
			required in fileSpec, string
			endparams
			record 
				snapShotNumber, int
			endrecord
		proc
			mreturn AddSnapShot(fileSpec,snapShotNumber)
		endmethod
		
		;;; <summary>
		;;; Returns information about the snapshots that exist in a file.
		;;; </summary>
		;;; <param name="fileSpec">File to retrieve snapshots for.</param>
		;;; <param name="snapshots">Returned collection of snapshots</param>
		;;; <returns>Returnd true if snapshot information was returned.</returns>
		public static method GetSnapshots, boolean
			required in  fileSpec, String
			required out snapshots, @ArrayList
			record 
				ok, boolean
			endrecord
		proc
			ok = true
			open(ch=0,i,"|ctutl -v " + fileSpec)

			;Change tracking information:
			;  There are 2 snapshots currently active
			;  (They are ordered oldest to most recent)
			;  (Change entries (Insert/Update/Delete/Ulink) occur between snapshots as shown)
            ;
			;  Beginning (#0) Fri Mar 07 14:27:41 2014
			;	0 change entries (0/0/0/0)
			;  Snapshot (#1) Fri Mar 07 14:33:57 2014
			;	0 change entries (0/0/0/0)
            ;

			reads(ch,buff)
			
			using buff select
			("Change tracking information:"),
				nop
			("%ISUTL-E-57: File not found"),
				ok = false
			("%ISUTL-E-STATUS: Change Tracking not enabled for "),
				ok = false
			(),
				ok = false
			endusing
			
			if (ok)
			begin

				;;Skip this, it will always be there
				
				;  There are XXX snapshots currently active
				;  (They are ordered oldest to most recent)
				;  (Change entries (Insert/Update/Delete/Ulink) occur between snapshots as shown)
				;

				reads(ch,buff)
				reads(ch,buff)
				reads(ch,buff)
				reads(ch,buff)
				
				;;Now we should be into any REAL snapshots. The first will look
				;;like this:
				
				;  Beginning (#XXXXX) Fri Mar 07 14:27:41 2014
				;	0 change entries (0/0/0/0)

				;;And subsequent ones will look like this:
				
				;  Snapshot (#XXXXX) Fri Mar 07 14:33:57 2014
				;    0 change entries (0/0/0/0)

				;;If we get a blank line we're done.
				
				snapshots = new ArrayList()

				repeat
				begin
					data pos, int
					data pos2, int
					data snapshotInfo, @Snapshot, new Snapshot()
					
					reads(ch,buff,eof)
					
					if (!buff)
						exitloop
					
					;;Make sure we have "  Beginning (#" or "  Snapshot (#"
					if (buff != "  Beginning (#") && (buff != "  Snapshot (#")
					begin
						ok = false
						exitloop
					end

					;;Find the # before the snapshot number
					if (!(pos = %instr(1,buff,"#")))
					begin
						ok = false
						exitloop
					end
					
					;;Find the ) after the snapshot number
					if (!(pos2 = %instr(pos,buff,")")))
					begin
						ok = false
						exitloop
					end

					;;Record the snapshot info
					snapshotInfo.SnapshotNumber = %integer(buff(pos+1,pos2-1))
					snapshotInfo.DateTimeTaken = ^d(buff(pos2+22:4)+%MonthNumber(buff(pos2+6:3))+buff(pos2+10:2)+buff(pos2+13:2)+buff(pos2+16:2)+buff(pos2+19:2))

					;;Get the second line
					reads(ch,buff)

					;    0 change entries (0/0/0/0)
					
					pos = %instr(5,buff," ")
					snapshotInfo.Changes = %integer(buff(5,pos-1))

					pos = %instr(1,buff,"(")
					pos2 = %instr(pos,buff,"/")
					snapshotInfo.Inserts = %integer(buff(pos+1,pos2-1))
					
					pos = pos2 + 1
					pos2 = %instr(pos,buff,"/")
					snapshotInfo.Updates = %integer(buff(pos,pos2-1))

					pos = pos2 + 1
					pos2 = %instr(pos,buff,"/")
					snapshotInfo.Deletes = %integer(buff(pos,pos2-1))
					
					pos = pos2 + 1
					pos2 = %instr(pos,buff,")")
					snapshotInfo.Unlinked = %integer(buff(pos,pos2-1))
					
					snapshots.Add(snapshotInfo)

				end
			end
eof,
			close ch

			mreturn ok

		endmethod
		
		public static method DeleteOldestSnapShots, boolean
			required in fileSpec, String
			required in snapShotsToDelete, int
			endparams
			record 
				ok, boolean
			endrecord
		proc
			ok = true
			open(ch=0,i,"|ctutl -f " + %string(snapShotsToDelete) + " " + fileSpec)

			;TODO: Check results!!!!

			close ch
			mreturn ok
		endmethod
		
		public static method DeleteOldestSnapShot, boolean
			required in fileSpec, String
			endparams
		proc
			mreturn DeleteOldestSnapShots(fileSpec,1)
		endmethod
		
		;;; <summary>
		;;; Deletes all but the latest snapshots in a file
		;;; </summary>
		;;; <param name="fileSpec">File to delete snapshots from</param>
		;;; <returns>Returns true if successfuk</returns>
		public static method DeleteOldSnapshots, boolean
			required in fileSpec, String
			endparams
			record 
				ok, boolean
			endrecord
		proc
			ok = true
			open(ch=0,i,"|ctutl -f -1 " + fileSpec)
			
			;;Example output:

			;Deleting 17 snapshots and freeing the following change entries:
			;  Beginning (#0) Fri Mar 07 14:27:41 2014
			;    0 snapshot (#1) change entries
			;  Snapshot (#1) Fri Mar 07 14:33:57 2014
			;    0 snapshot (#2) change entries
			;  Snapshot (#2) Fri Mar 07 16:29:46 2014
			;    0 snapshot (#3) change entries
			;  Snapshot (#3) Fri Mar 07 16:29:50 2014
			;    0 snapshot (#4) change entries
			;  Snapshot (#4) Fri Mar 07 16:29:54 2014
			;    0 snapshot (#5) change entries
			;  Snapshot (#5) Fri Mar 07 16:38:18 2014
			;    0 snapshot (#6) change entries
			;  Snapshot (#6) Fri Mar 07 16:38:40 2014
			;    0 snapshot (#7) change entries
			;  Snapshot (#7) Fri Mar 07 16:38:52 2014
			;    0 snapshot (#8) change entries
			;  Snapshot (#8) Fri Mar 07 16:42:26 2014
			;    0 snapshot (#9) change entries
			;  Snapshot (#9) Wed Apr 23 10:55:02 2014
			;    0 snapshot (#10) change entries
			;  Snapshot (#10) Wed Apr 23 10:56:27 2014
			;    0 snapshot (#11) change entries
			;  Snapshot (#11) Wed Apr 23 11:29:18 2014
			;    0 snapshot (#12) change entries
			;  Snapshot (#12) Wed Apr 23 11:29:37 2014
			;    0 snapshot (#13) change entries
			;  Snapshot (#13) Wed Apr 23 11:35:18 2014
			;    0 snapshot (#14) change entries
			;  Snapshot (#14) Wed Apr 23 11:49:26 2014
			;    0 snapshot (#15) change entries
			;  Snapshot (#15) Wed Apr 23 12:09:40 2014
			;    0 snapshot (#16) change entries
			;  Snapshot (#16) Fri Apr 25 11:52:45 2014
			;    0 snapshot (#17) change entries
			;Deleting snapshot (#0)
			;Deleting snapshot (#1)
			;Deleting snapshot (#2)
			;Deleting snapshot (#3)
			;Deleting snapshot (#4)
			;Deleting snapshot (#5)
			;Deleting snapshot (#6)
			;Deleting snapshot (#7)
			;Deleting snapshot (#8)
			;Deleting snapshot (#9)
			;Deleting snapshot (#10)
			;Deleting snapshot (#11)
			;Deleting snapshot (#12)
			;Deleting snapshot (#13)
			;Deleting snapshot (#14)
			;Deleting snapshot (#15)
			;Deleting snapshot (#16)
			
			;If there is only one snapshot:
			;There are only 1 snapshots active, free ignored
			
			;;Get the response
			reads(ch,buff)

			using buff select
			("Deleting "),
			begin
				nop
			end
			("There are only 1 snapshots active, free ignored"),
			begin
				nop
			end
			("There are no changes entries to free"),
			begin
				nop
			end
			("%ISUTL-E-SYNC: Change Tracking not enabled for "),
			begin
				ok = false
			end
			endusing
			
			close ch
			mreturn ok
		endmethod
		
	endclass

endnamespace
