;;*****************************************************************************
;; Title:       menu.dbl
;;
;; Author:      Steve Ives, Synergex Professional Services Group
;;
;; Description: A simple menu system to execute generated programs
;;
;******************************************************************************

main menu

    .include "WND:tools.def"
    .include "WND:inpctl.def"

    .define APP_TITLE   "SQL Upload Net Change Demo System Menu"
    .define APP_VERSION "V1.0"

    .define SYSTEM_DATA_INIT
    .include "LIBSRC:system.def"
	.include "LIBSRC:StructureIO.def"

    record
        mbcontrol       ,a1000      ;Menu build buffer

proc

    call setup

    repeat
    begin
        xcall m_process

        using g_entnam select
        ("EXIT"),
            exitloop
        ("ABOUT"),
            xcall u_about(APP_TITLE,APP_VERSION,%datecompiled)
        ("EMPMNT"),
        begin
			data channel, i4
			;;Open the employee master file
			if (%EmployeeIO(IO_OPEN_UPD,channel)==IO_OK) then
			begin
				;;Connect the IO hooks to ensure that hire date is populated on store
				new EmployeeMaintenanceHooks(channel)
				xcall e_enter
				xcall m_disable(D_COLUMN,idc_apps)
				xcall EmployeeMaintTab("Employee",channel,"ID|First Name|Last Name|Department|")
				xcall e_exit
			end
			else
				xcall u_msgbox("Failed to open file DAT:employee.ism",D_MOK+D_MICONSTOP,"Error")
        end
        endusing
	end

    xcall u_finish
    stop

;;------------------------------------------------------------------------------
;;Get Toolkit up and running
;;
setup,

    ;;Start UI Toolkit
    xcall u_start("EXE:tkapp.ism",1,0,,30,100)

    ;;Configure Toolkit environment
    xcall e_sect(APP_TITLE,D_HEADER,D_CENTER)
    xcall e_state(D_ON,D_RETURNBTN,D_VALSTCHG)
    xcall e_method(D_METH_ENTRST,"entrst_method")
    xcall u_wndfont(D_SETFONT,DF_CURRENT,"MS Sans Serif",10,"A")
    xcall u_wndfont(D_SETFONT,DF_ALTERNATE,"MS Sans Serif",10,"A")

    ;;Create window event mehod sets
    wndevent_close = %u_wndevents(D_CREATE,,D_EVENT_CLOSE,"wndevent_close")

    ;;Build file menu
    xcall mb_column(mbcontrol,"FILE","File",D_GLOBAL)
    xcall mb_entry(mbcontrol,"ABOUT","About",F1_KEY)
    xcall mb_entry(mbcontrol,"EXIT","Exit",F4_KEY)
    xcall mb_end(mbcontrol,idc_file)

    ;;Build edit menu
    xcall mb_column(mbcontrol,"EDIT","Edit",D_GLOBAL)
    xcall mb_entry(mbcontrol,"E_CUT","Cut",CTRL_X_KEY,,"T",,1)
    xcall mb_entry(mbcontrol,"E_COPY","Copy",CTRL_C_KEY,,"C",,1)
    xcall mb_entry(mbcontrol,"E_PASTE","Paste",CTRL_V_KEY,,"P",,1)
    xcall mb_end(mbcontrol,idc_edit)

    ;;Build applications menu
    xcall mb_column(mbcontrol,"APPS","Applications")
    xcall mb_entry(mbcontrol,"EMPMNT","Employee Maintenance")
    xcall mb_end(mbcontrol,idc_apps)

    ;;Process the menu
    .ifdef D_GUI
    xcall m_defcol(0)
    .else
    xcall m_defcol(3)
    .endc

    return

endmain

import Synergex.SynergyDE.IOExtensions

namespace ApplicationIoHooks

    public sealed class EmployeeMaintenanceHooks extends IOHooks
		
		public method EmployeeMaintenanceHooks
			required in channel, i4
			endparams
			parent(channel)
		proc

		endmethod
		
		public override method store_pre_operation_hook, void
			required inout aBuffer,  a
			required in    aIoFlags, IOFlags
			endparams
			.include "EMPLOYEE" repository, record="employee"
			record 
				today, a8
			endrecord
		proc
			today = %datetime
			employee = aBuffer
			employee.emp_hire_date = ^d(today)
			aBuffer = employee
		endmethod

    endclass

endnamespace
