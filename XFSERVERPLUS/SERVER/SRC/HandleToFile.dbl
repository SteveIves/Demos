;*******************************************************************************
;
; Title:        HandleToFile.dbl
;
; Type:         Function
;
; Description:  Saves binary data from a memory handle to a local file
;
; Author:       Steve Ives, Synergex Professional Services Group
;
; Copyright:    ©Synergex International Inc.  All rights reserved.
;
; WARNING:      If you were given this code by a Synergex employee then you may
;               use and modify it freely within your own applications. However,
;               you may not under any circumstances distribute this code, or
;               any modified version of this code, to any third party without
;               first obtaining written permission to do so from Synergex.
;               In using this code you accept that it is provided as is, and
;               without support or warranty.  Neither Synergex or the author
;               accept any responsibility for any losses or damages of any
;               nature which may arise from the use of this code.  This header
;               information must remain unaltered in the code at all times.
;               Possession of this code, or any modified version of this code,
;               indicates your acceptance of these terms.
;
; $Revision: $
;
; $Date: $
;
;*******************************************************************************
.ifndef OS_VMS ;WINDOWS AND UNIX CODE
;*******************************************************************************

function HandleToFile ,^VAL

    required in a_mh    ,int         ;Memory handle
    required in a_file  ,a           ;File name to load
    endparams

    stack record ivars
        retval      ,int             ;Routine return value
        ch          ,int             ;Output file channel
        chars       ,int             ;File size (bytes)
        chunks      ,int             ;Number of chunks to write
        count       ,int             ;Loop counter
        remaining   ,int             ;Chars remaining after full chunks written
    endrecord

    .define D_BUFSZ 32768
    
    structure chunk
                    ,a D_BUFSZ
    endstructure
    
    stack record avars
        buf         ,a D_BUFSZ      ;IO buffer
    endrecord

.proc

    init ivars, avars

    ;Open the input file
    open(ch=%syn_freechn,o,a_file) [ERR=nf]

    if 0
nf,     retval=1

    if (!retval)
    begin
        ;Find out how much data we're dealing with, and calculate how many
        ;puts will be required based on the current vlock size.
        chars = %mem_proc(DM_GETSIZE,a_mh)
        chunks = (chars/D_BUFSZ)

        ;Write full chunks to file
        for count from 1 thru chunks
            puts(ch,^m(chunk[count],a_mh))

        ;Write remaining data to file
        remaining = (chars-(chunks*D_BUFSZ))
        if (remaining)
            puts(ch,^m(chunk[count](1:remaining),a_mh))
    end

    ;Close file
    if (retval!=1)
        close ch

    freturn retval

endfunction
;
;*******************************************************************************
.else ;VMS CODE
;*******************************************************************************
;
;THIS CODE WILL NOT WORK YET!
;
function HandleToFile ,^VAL

    required in a_mh        ,int              ;Memory handle
    required in a_file      ,a              ;File name to load
    endparams

    stack record ivars
        retval      ,int             ;Routine return value
        ch          ,int             ;Output file channel
        chars       ,int             ;File size (bytes)
        chunks      ,int             ;Number of chunks to write
        count       ,int             ;Loop counter
        remaining   ,int             ;Chars remaining after full chunks written
    endrecord

    .define D_BUFSZ 512
    
    structure chunk
                    ,a D_BUFSZ
    endstructure
    
    stack record avars
        buf         ,a D_BUFSZ      ;IO buffer
    endrecord
    
proc

    init ivars, avars

    ;Open the input file
    open(ch=%syn_freechn,o:b,a_file) [ERR=nf]

    if 0
nf,     retval=1

    if (!retval)
    begin
        ;Find out how much data we're dealing with, and calculate how many
        ;puts will be required based on the current vlock size.
        chars = %mem_proc(DM_GETSIZE,a_mh)
        chunks = chars / D_BUFSZ

        ;Write full chunks to file
        for count from 1 thru chunks
            puts(ch,^m(chunk[count],a_mh))

        ;Write remaining chunks to file
        remaining = (chars-(chunks*D_BUFSZ))
        if (remaining)
            puts(ch,^m(chunk[count](1:remaining),a_mh))
    end

    ;Close file
    if (retval!=1)
        close ch

    freturn retval

endfunction
;*******************************************************************************
.endc ;END OF VMS CODE
;*******************************************************************************

