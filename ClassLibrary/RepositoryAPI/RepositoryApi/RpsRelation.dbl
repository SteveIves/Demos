;;*****************************************************************************
;;
;; Class:		SynPsg.Rps.RpsRelation
;;
;; Description:	Represents a single relation definition for a Repository structure
;;
;; Author:		Steve Ives, Synergex Professional Services Group
;;
;; Date: 		19th October 2007
;;
;; ****************************************************************************
;;
;; This code is supplied as seen and without warranty or support, and is used
;; at your own risk. Neither the author or Synergex accept any responsability
;; for any loss or damage which may result from the use of this code.
;;
;; ****************************************************************************
;;
;; $Revision: 3 $
;;
;; $Date: 2011-06-09 16:05:17-07:00 $
;;
;; ****************************************************************************
;;
.define DDINFO_DEFINES_ONLY
.include "RPSLIB:ddinfo.def"
.undefine DDINFO_DEFINES_ONLY

import SynPsg.Rps

namespace SynPsg.Rps

	public class RpsRelation

		.region "Private data"
		
		protected p_RelationName	,String
		protected p_FromKey			,a30
		protected p_ToStructure		,a30
		protected p_ToKey			,a30

		.endregion
		
		.region "Constructirs and destructors"

		public method RpsRelation
			required in StructureName, String
			required in RelationName, String
			endparams
		proc
			Repository.CheckIsOpen()
			LoadRelation(StructureName,RelationName)
		endmethod

		.endregion

		.region "Private methods"

		private method LoadRelation, void
			required in StructureName, String
			required in RelationName, String
			endparams
		proc
			;;Save the relation name
			p_RelationName = RelationName.Trim().ToUpper()
			begin
				data p_s_info, s_info
				;;Set the current structure
				dd_struct(Repository.RpsControl,DDS_INFO,StructureName.Trim().ToUpper(),p_s_info)
				if (Repository.RpsControl.error)
					throw new RpsRelationException("Failed to set current repository structure to "+StructureName)
				;;Load the relation information
				dd_relation(Repository.RpsControl,DDR_INFO,p_RelationName,p_FromKey,p_ToStructure,p_ToKey)
				if (Repository.RpsControl.error)
					throw new RpsRelationException(string.Format("Failed to get information for repository relation {0}.{1}",StructureName,RelationName))
			end
		endmethod
		
		.endregion

		.region "Public properties"

		;Relation name
		public property Name, String
			method get
			proc
				mreturn p_RelationName
			endmethod
		endproperty
		
		;;From key
		public property FromKey, String
			method get
			proc
				mreturn atrim(p_FromKey)
			endmethod
		endproperty

		;;To structure
		public property ToStructure, String
			method get
			proc
				mreturn atrim(p_ToStructure)
			endmethod
		endproperty

		;;To key
		public property ToKey, String
			method get
			proc
				mreturn atrim(p_ToKey)
			endmethod
		endproperty

		.endregion

	endclass

endnamespace

