;*******************************************************************************
;
; Title:        GetAllProducts.dbl
;
; Type:         Method
;
; Description:  Returns a collection of all product records via dynamic memory.
;
; Author:       Steve Ives, Synergex Professional Services Group
;
;*******************************************************************************
;

import Synergex.SynergyDE.Select

{xfMethod(interface="SynergyMethods",elb="EXE:SynergyServer")}

;;;<summary>Retrieves a collection of all products.</summary>
;;;<returns>Returns the number of products retrieved.</returns>
;;;<param name="products">Returned collection of products.</param>

function GetAllProducts, ^VAL

	{xfParameter(name="products",collectionType=xfCollectType.structure,structure="strProduct")}
	required out a_products	,D_HANDLE

	endparams

	stack record local_data
		allocated	,i4
		used		,i4
		product		,strProduct
	endrecord

proc

	init local_data

	foreach product in new Select(
	&	new From("DAT:product.ism",product)		;Records from the product file
	&	)
	begin
		;;Make sure we have room for it
		if ((used+=1)>allocated)
			a_products = %mem_proc(DM_RESIZ,^size(strProduct)*(allocated+=100),a_products)
		;;Save it
		^m(strProduct[used],a_products) = product
	end
	
	freturn used

endfunction

