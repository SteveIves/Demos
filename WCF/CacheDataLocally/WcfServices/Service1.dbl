import System
import System.Collections.ObjectModel
import System.Runtime.Serialization
import System.ServiceModel
import System.Text

namespace WcfServices
	
	public class Service1 implements IService1
		
		public method GetPerson, Boolean
			required in  aPersonId	,int
			required out aPerson	,@Person
			endparams
			stack record local_data
				ch		,int
				k		,d8
				p		,strPerson
				ok		,Boolean
			endrecord
		proc
			init local_data
			try
			begin
				open(ch,i:i,"DAT:person.ism")
				read(ch,p,k=aPersonId)
				aPerson = new Person(p)
				ok = true
			end
			catch (ex, @EndOfFileException)
			begin
				aPerson = new Person()
			end
			catch (ex, @KeyNotSameException)
			begin
				aPerson = new Person()
			end
			catch (ex, @Exception)
			begin
				aPerson = new Person()
			end
			finally
			begin
				if (ch&&%chopen(ch))
					close ch
			end
			endtry
			mreturn ok
		endmethod
		
		public method GetAllPeople, Boolean
            required out aPeople, @ObservableCollection<Person>
			endparams
			stack record local_data
				ch		,int
				p		,strPerson
				ok		,Boolean
			endrecord
		proc
			init local_data
            aPeople = new ObservableCollection<Person>()
			try
			begin
				open(ch,i:i,"DAT:person.ism")
				repeat
				begin
					reads(ch,p)
					aPeople.Add(new Person(p))
				end
			end
			catch (ex, @EndOfFileException)
			begin
				ok = true
			end
			catch (ex, @Exception)
			begin
				nop
			end
			finally
			begin
				if (ch&&%chopen(ch))
					close ch
			end
			endtry
			mreturn ok
		endmethod
		
	endclass
	
endnamespace


