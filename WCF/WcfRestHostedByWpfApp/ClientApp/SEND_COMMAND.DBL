subroutine send_command
	ip_address	,a
	msgid		,a
	msgdata		,a
	ok			,d
	errmsg		,a
	endparams
	
	.include 'DBLDIR:synxml.def'
	
	record
		errnum  	,int
		timeout		,int
		errtxt		,string
		url     	,string			;;URL for operation
		msgurl		,string
		request		,string			;;String sent
		response	,string			;;String received
		hdr     	,[#]string		;;Headers sent
		rhdr    	,[#]string		;;Headers received
	endrecord
	
proc
	url = 'http://192.168.9.1:8088/LocalAppService/messages'
	timeout = 5
	
	;;Process message
	msgurl = url + "/" + msgid

	if (msgid=="EXIT") then 
		request = '""'
	else
	begin
		data tmpmsg, string, %atrim(msgdata)
		tmpmsg = tmpmsg.Replace('\','\\').Replace('"','\"')
		request = '"' + tmpmsg + '"'
	end

	;;Configure HTTP headers
	hdr = new string[4]
	hdr[1] = 'Host: ' + ip_address + ':8088'
	hdr[2] = 'Content-Type: application/json'
	hdr[3] = 'Content-Length: ' + %string(request.Length)
	hdr[4] = 'Accept: application/json'

	;;Send the HTTP POST request
	errnum = http_post(msgurl,timeout,request,response,errtxt,hdr,rhdr,'http.log',,,,,HTTP_RELURI,'1.1')

	;;Process the return value from the service
	using errnum select
	(0),
	begin
		ok = 1
		errmsg = ""
	end
	(10054),
	begin
		;;Connection reset. Depending on timing we sometimes get this response
		;;;when we send an EXIT message!
		if (msgid=="EXIT") then 
		begin
			ok = 1
			errmsg = ""
		end
		else
		begin
			ok = 0
			errmsg = 'ERROR 10054 ' + errtxt
		end
	end
	(10061),
	begin
		ok = 0
		errmsg = 'ERROR 10061 ' + errtxt
	end
	(),
	begin
		ok = 0
		errmsg = 'ERROR ' + %string(errnum) + ' ' + errtxt
	end
	endusing

	xreturn
	
endsubroutine

